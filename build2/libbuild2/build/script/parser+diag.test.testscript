# file      : libbuild2/build/script/parser+diag.test.testscript
# license   : MIT; see accompanying LICENSE file

test.options += -g

: name
:
$* test <<EOI >>EOO
  echo abc
  EOI
  name: test
  EOO

: name-deduce
:
$* <<EOI >>EOO
  echo abc
  EOI
  name: echo
  EOO

: diag
:
$* <<EOI >>~%EOO%
  echo abc
  cat abc
  diag copy >= $>
  cp <- $>
  EOI
  %diag: copy >= .+file\{driver\.\}%
  EOO

: ambiguity
:
{
: name
:
  $* test <<EOI 2>>EOE != 0
    echo abc
    diag xyz
    EOI
    buildfile:12:1: error: both low-verbosity script diagnostics name and 'diag' builtin call
      buildfile:10: info: script name specified here
    EOE

  : diag
  :
  $* <<EOI 2>>EOE != 0
    echo abc
    diag abc
    cat abc
    diag xyz
    EOI
    buildfile:14:1: error: multiple 'diag' builtin calls
      buildfile:12:1: info: previous call is here
    EOE
}

: inside-if
:
$* <<EOI 2>>EOE != 0
  if true
    diag copy >= $>
  fi
  EOI
  buildfile:12:3: error: 'diag' call inside flow control construct
  EOE

: inside-if-cond
:
$* <<EOI 2>>EOE != 0
  if diag copy >= $>
    true
  fi
  EOI
  buildfile:11:4: error: 'diag' call inside flow control construct
  EOE

: second-command
:
$* <<EOI 2>>EOE != 0
  true && diag copy >= $>
  EOI
  buildfile:11:9: error: 'diag' call must be the only command
  EOE

: via-env
:
$* <<EOI 2>>EOE != 0
  env -- diag copy >= $>
  EOI
  buildfile:11:8: error: 'diag' call via 'env' builtin
  EOE
