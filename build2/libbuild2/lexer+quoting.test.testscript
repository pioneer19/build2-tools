# file      : libbuild2/lexer+quoting.test.testscript
# license   : MIT; see accompanying LICENSE file

test.options += -q

: unquoted
:
$* <'foo' >>EOO
'foo'
<newline>
EOO

: comp
:
{
  : single
  :
  $* <":'foo':" >>EOO
  :
  'foo' [S/C]
  :
  <newline>
  EOO

  : double
  :
  $* <':"foo":' >>EOO
  :
  'foo' [D/C]
  :
  <newline>
  EOO

  : single-empty
  :
  $* <"''" >>EOO
  '' [S/C]
  <newline>
  EOO

  : double-empty
  :
  $* <'""' >>EOO
  '' [D/C]
  <newline>
  EOO
}

: part
:
{
  : quoted
  :
  {
    : start
    : Token start already quoted
    :
    $* <'"$foo"' >>EOO
    '' [D/P]
    $ [D/C]
    'foo' [D/P]
    <newline>
    EOO

    : end
    : Token end still quoted
    :
    $* <'"foo$"' >>EOO
    'foo' [D/P]
    $ [D/C]
    '' [D/P]
    <newline>
    EOO
  }

  : unquoted
  {
    : start
    : Token starts with unquoted character
    :
    $* <'f"oo"' >>EOO
    'foo' [D/P]
    <newline>
    EOO

    : end
    : Token continous with unquoted character
    :
    $* <'"fo"o' >>EOO
    'foo' [D/P]
    <newline>
    EOO

    : escape
    : Token continous with unquoted escaped character
    :
    $* <'"fo"\"' >>EOO
    'fo"' [D/P]
    <newline>
    EOO
  }
}

: mixed
:
$* <"\"fo\"'o'" >>EOO
'foo' [M/P]
<newline>
EOO
